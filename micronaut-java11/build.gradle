plugins {
    id("java")
    id("com.github.johnrengelman.shadow") version("6.1.0")
    id("com.dorongold.task-tree") version("1.5")
    id("com.github.ben-manes.versions") version("0.38.0")
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "http://dynamodb-local.s3-website-us-west-2.amazonaws.com/release"
    }
}

configurations {
    developmentOnly
    dynamodb
}

dependencies {
    /* Lombok */
    def lombokVersion = "1.18.18"
    compileOnly("org.projectlombok:lombok:${lombokVersion}")
    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")

    /* DynamoDB */
    def dynamoDbVersion = "2.16.18"
    def dynamoDbLocalVersion = "1.15.0"
    implementation("software.amazon.awssdk:dynamodb:${dynamoDbVersion}")
    implementation("software.amazon.awssdk:url-connection-client:${dynamoDbVersion}")
    testImplementation("com.amazonaws:DynamoDBLocal:${dynamoDbLocalVersion}")
    dynamodb(fileTree(dir: "lib", include: ["*.dylib", "*.so", "*.dll"]))
    dynamodb("com.amazonaws:DynamoDBLocal:${dynamoDbLocalVersion}")

    /* Micronaut Framework */
    def micronautVersion = "2.4.0"
    annotationProcessor(platform("io.micronaut:micronaut-bom:${micronautVersion}"))
    annotationProcessor("io.micronaut:micronaut-inject-java")
    implementation(platform("io.micronaut:micronaut-bom:${micronautVersion}"))
    implementation("io.micronaut:micronaut-inject")
    implementation("io.micronaut:micronaut-runtime")
    implementation("javax.annotation:javax.annotation-api")
    implementation("io.micronaut.aws:micronaut-aws-sdk-v2")
    implementation("io.micronaut.aws:micronaut-function-aws-api-proxy")
    implementation("io.micronaut.aws:micronaut-function-aws-custom-runtime")
    runtimeOnly("ch.qos.logback:logback-classic")
    testAnnotationProcessor(enforcedPlatform("io.micronaut:micronaut-bom:${micronautVersion}"))
    testAnnotationProcessor("io.micronaut:micronaut-inject-java")
    testImplementation(enforcedPlatform("io.micronaut:micronaut-bom:${micronautVersion}"))
    testImplementation("io.micronaut.test:micronaut-test-junit5")
    testImplementation("io.micronaut.aws:micronaut-function-aws-test")

    /* Testing */
    def junitVersion = "5.8.0-M1"
    def assertJVersion = "3.19.0"
    testImplementation("org.junit.jupiter:junit-jupiter:${junitVersion}")
    testImplementation("org.assertj:assertj-core:${assertJVersion}")
}

task copyNativeDeps(type: Copy) {
    from(configurations.dynamodb)
    into("$project.buildDir/libs")
}

test {
    classpath += configurations.developmentOnly
    useJUnitPlatform()
    testLogging {
        events = ['FAILED', 'PASSED', 'SKIPPED', 'STANDARD_OUT']
    }
    dependsOn copyNativeDeps
    doFirst {
        systemProperty "java.library.path", "build/libs"
    }

    // Required by the AWS API
    environment "AWS_REGION", "eu-central-1"
}

shadowJar {
    mergeServiceFiles()
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs.addAll([
            "-parameters",
            // enables incremental compilation
            "-Amicronaut.processing.incremental=true",
            "-Amicronaut.processing.annotations=demo.albertattard.uniquecode.*",
            "-Amicronaut.processing.group=${project.group}",
            "-Amicronaut.processing.module=${project.name}",
    ])
}

tasks.withType(JavaExec) {
    classpath += configurations.developmentOnly
    jvmArgs("-XX:TieredStopAtLevel=1", "-Dcom.sun.management.jmxremote")
    if (gradle.startParameter.continuous) {
        systemProperties(
                "micronaut.io.watch.restart": "true",
                "micronaut.io.watch.enabled": "true",
                "micronaut.io.watch.paths": "src/main"
        )
    }
}
